/obj/effect/proc_holder/ability
	name = "ability"
	desc = "An ability."
	panel = "Abilities"
	anchored = TRUE
	pass_flags = PASSTABLE
	density = FALSE
	opacity = FALSE

	action_icon = 'icons/mob/actions/actions_ability.dmi'
	action_icon_state = "default"
	action_background_icon_state = "bg_spell"
	base_action = /datum/action/spell_action/ability/item

	var/stat_allowed = FALSE
	/// Current cooldown.
	var/cooldown = 0
	/// Time added to cooldown on use.
	var/cooldown_time = 0
	//How many charges of this ability we have that dont require cooldown
	// null means we dont use this mechanic.
	var/abil_charges = 0
	//Only contains tags or text identifiers
	var/list/hit_identifiers = list()

/obj/effect/proc_holder/ability/Initialize()
	. = ..()
	START_PROCESSING(SSfastprocess, src)

/obj/effect/proc_holder/ability/Destroy()
	STOP_PROCESSING(SSfastprocess, src)
	qdel(action)
	return ..()

/obj/effect/proc_holder/ability/process(delta_time)
	if(action && world.time > cooldown)
		action.UpdateButtonIcon()

/obj/effect/proc_holder/ability/Click()
	var/mob/living/user = usr
	if(!istype(user))
		return
	if(!can_cast(user))
		return
	Perform(null, user)

/obj/effect/proc_holder/ability/update_icon()
	if(!action)
		return
	action.UpdateButtonIcon()

/obj/effect/proc_holder/ability/proc/can_cast(mob/user = usr)
	if(cooldown > world.time &&  abil_charges < 1)
		return FALSE

	if(!action || action.owner != user)
		return FALSE

	if(user.stat && !stat_allowed)
		return FALSE

	if(user.incapacitated())
		return FALSE

	return TRUE

/obj/effect/proc_holder/ability/proc/Perform(target, user)
	cooldown = world.time + cooldown_time

	if(abil_charges != null && abil_charges > 0)
		cooldown = 0
		abil_charges--
		update_icon()

	if(cooldown_time > 0)
		remove_ranged_ability()
	update_icon()
	return

/obj/effect/proc_holder/ability/aimed/Click()
	var/mob/living/user = usr
	if(!istype(user))
		return
	var/msg
	if(!can_cast(user))
		remove_ranged_ability()
		return
	if(active)
		msg = "<span class='notice'>You decide not to use [src].</span>"
		remove_ranged_ability(msg)
		on_deactivation(user)
	else
		msg = "<span class='notice'><B>Left-click to perform the ability!</B></span>"
		add_ranged_ability(user, msg, TRUE)
		on_activation(user)

/obj/effect/proc_holder/ability/aimed/proc/on_activation(mob/user)
	return

/obj/effect/proc_holder/ability/aimed/proc/on_deactivation(mob/user)
	return

//Easy way of handling immobalization for humans and mobs.
/obj/effect/proc_holder/ability/proc/ToggleAct(mob/living/dude, status = FALSE)
	if(ishostile(dude))
		var/mob/living/simple_animal/hostile/hos = dude
		hos.can_act = status

//Flicks a overlay on a object. Seemed like a cheaper option for stationary effects.
/obj/effect/proc_holder/ability/proc/FlickOnAtom(atom/A, icon_file, icon_file_state, flicktime = 10)
	var/image/effect_flick = image(icon_file,A,icon_file_state,CLOSED_FIREDOOR_LAYER)
	effect_flick.plane = GAME_PLANE
	effect_flick.appearance_flags = APPEARANCE_UI_IGNORE_ALPHA
	flick_overlay_view(effect_flick, A, flicktime)
	return effect_flick

//Returns true if the identifier is in the list, false if not and automatically adds.
/obj/effect/proc_holder/ability/proc/HasIdentList(atom/curwibble)
	var/identifer = AddIdentifier(curwibble)
	if(identifer in hit_identifiers)
		return TRUE
	hit_identifiers += identifer
	return FALSE

/obj/effect/proc_holder/ability/proc/AddIdentifier(atom/whazzit)
	if(isturf(whazzit))
		var/turf/U = whazzit
		return "[U.x],[U.y],[U.z]"
	if(isliving(whazzit))
		var/mob/living/dude = whazzit
		return dude.tag
	if(isvehicle(whazzit))
		var/obj/vehicle/ride = whazzit
		var/following_ident = "[ride.x],[ride.y],[ride.z]"
		var/driver = ride.return_drivers()
		if(isliving(driver))
			var/mob/living/guy = driver
			following_ident = "[guy.tag]"

		//Only identify by the driver
		return "[ride]:[following_ident]"

//Unique interactions with simplemobs such as var alterations
/obj/effect/proc_holder/ability/proc/AbnoInteraction(mob/living/user)
	return

/obj/effect/proc_holder/ability/proc/AlterCharge(amt)
	if(abil_charges == -1)
		return
	cooldown = 0
	abil_charges += amt

/obj/effect/proc_holder/ability/aimed/update_icon()
	if(!action)
		return
	action.button_icon_state = "[base_icon_state][active]"
	action.UpdateButtonIcon()

/obj/effect/proc_holder/ability/aimed/InterceptClickOn(mob/living/requester, params, atom/target)
	if(..())
		return FALSE
	if(!can_cast())
		remove_ranged_ability()
		return FALSE
	Perform(target, user = ranged_ability_user)
	return TRUE

/obj/effect/proc_holder/ability/hat_ability
	name = "Toggle Hat"
	desc = "Toggle your current armors hat."
	action_icon_state = "hat0"
	base_icon_state = "hat"
	var/obj/item/clothing/head/ego_hat/hat = null

/obj/effect/proc_holder/ability/hat_ability/New(loc, obj/item/clothing/head/ego_hat/ego_hat, ...)
	. = ..()
	hat = ego_hat

/obj/effect/proc_holder/ability/hat_ability/Perform(target, user)
	. = ..()
	if(!ishuman(user))
		return
	if(isnull(hat))
		Destroy()
		return
	var/mob/living/carbon/human/H = user
	var/obj/item/clothing/head/headgear = H.get_item_by_slot(ITEM_SLOT_HEAD)
	if(!istype(headgear, hat)) // We don't have the hat on?
		if(!isnull(headgear))
			if(HAS_TRAIT(headgear, TRAIT_NODROP))
				to_chat(H, "<span class='warning'>[headgear] cannot be dropped!</span>")
				return
			H.dropItemToGround(headgear) // Drop the other hat, if it exists.
		H.equip_to_slot(new hat, ITEM_SLOT_HEAD) // Equip the hat!
		return
	headgear.Destroy()
	return

/obj/effect/proc_holder/ability/neck_ability
	name = "Toggle Neckwear"
	desc = "Toggle your current armors neckwear."
	action_icon_state = "neck0"
	base_icon_state = "neck"
	var/obj/item/clothing/neck/ego_neck/neck = null

/obj/effect/proc_holder/ability/neck_ability/New(loc, obj/item/clothing/neck/ego_neck/ego_neck, ...)
	. = ..()
	neck = ego_neck

/obj/effect/proc_holder/ability/neck_ability/Perform(target, user) // Works just like the hat ability from above
	. = ..()
	if(!ishuman(user))
		return
	if(isnull(neck))
		Destroy()
		return
	var/mob/living/carbon/human/H = user
	var/obj/item/clothing/neck/neckwear = H.get_item_by_slot(ITEM_SLOT_NECK)
	if(!istype(neckwear, neck))
		if(!isnull(neckwear))
			if(HAS_TRAIT(neckwear, TRAIT_NODROP))
				to_chat(H, "<span class='warning'>[neckwear] cannot be dropped!</span>")
				return
			H.dropItemToGround(neckwear )
		H.equip_to_slot(new neck, ITEM_SLOT_NECK)
		return
	neckwear.Destroy()
	return

/obj/effect/proc_holder/ability/mask_ability
	name = "Toggle Mask"
	desc = "Toggle your current armors mask."
	action_icon_state = "mask0"
	base_icon_state = "mask"
	var/obj/item/clothing/mask/ego_mask/mask = null

/obj/effect/proc_holder/ability/mask_ability/New(loc, obj/item/clothing/mask/ego_mask/ego_mask, ...)
	. = ..()
	mask = ego_mask

/obj/effect/proc_holder/ability/mask_ability/Perform(target, user) // Works just like the neck ability from above
	. = ..()
	if(!ishuman(user))
		return
	if(isnull(mask))
		Destroy()
		return
	var/mob/living/carbon/human/H = user
	var/obj/item/clothing/mask/maskwear = H.get_item_by_slot(ITEM_SLOT_MASK)
	if(!istype(maskwear, mask))
		if(!isnull(maskwear))
			if(HAS_TRAIT(maskwear, TRAIT_NODROP))
				to_chat(H, "<span class='warning'>[maskwear] cannot be dropped!</span>")
				return
			H.dropItemToGround(maskwear)
		H.equip_to_slot(new mask, ITEM_SLOT_MASK)
		return
	maskwear.Destroy()
	return
